buildscript {
    ext {
        kotlin_version = '1.2.40'
        okhttp_version = '3.10.0'

        spongycastleVersion = '1.58.0.0'
        slf4jVersion = '1.7.25'
        khex_version = '0.6'

    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath files('libs/gradle-witness.jar')
    }
}

subprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    apply plugin: "kotlin"
    apply plugin: "jacoco"
    apply plugin: "maven"
    apply plugin: "com.github.ben-manes.versions"

    afterEvaluate { project ->

        dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

            testCompile 'org.assertj:assertj-core:3.9.1'
            testCompile 'junit:junit:4.12'
            testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        }

        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        artifacts {
            archives sourcesJar
            archives javadocJar
        }
    }
}
